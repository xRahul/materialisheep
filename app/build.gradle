apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'

android {
    compileSdk 36

    defaultConfig {
        applicationId "io.github.sheepdestroyer.materialisheep"
        minSdkVersion 24
        targetSdkVersion 36
        versionCode 79
        def gitHash = { ->
            try {
                def hash = "git rev-parse --short HEAD".execute().text.trim()
                return hash.isEmpty() ? "unknown" : hash
            } catch (Exception e) {
                "unknown"
            }
        }()
        versionName "3.3-" + gitHash
        buildConfigField "String", "GIT_HASH", "\"${gitHash}\""
        buildConfigField "int", "LATEST_RELEASE", "77"
        buildConfigField "String", "GITHUB_TOKEN", "\"\""
        proguardFiles getDefaultProguardFile('proguard-android.txt'),
                'proguard-rules.pro',
                'proguard-square.pro',
                'proguard-support.pro',
                'proguard-rx.pro'
        resConfigs 'en', 'zh-rCN', 'es'
    }

    signingConfigs {
        release {}
    }

    def keystoreBase64 = System.getenv("KEY_STORE_BASE64")
    def keystorePassword = System.getenv("KEY_STORE_PASSWORD")
    def keyAlias = System.getenv("KEY_ALIAS")
    def keyPassword = System.getenv("KEY_PASSWORD")

    if (keystoreBase64 != null && keystorePassword != null && keyAlias != null && keyPassword != null) {
        def keystoreFile = new File(layout.buildDirectory.get().asFile, "release.jks")
        keystoreFile.bytes = keystoreBase64.decodeBase64()

        signingConfigs.release.storeFile = keystoreFile
        signingConfigs.release.storePassword = keystorePassword
        signingConfigs.release.keyAlias = keyAlias
        signingConfigs.release.keyPassword = keyPassword
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            }
        }
    }
    lintOptions {
        htmlReport false
        xmlReport false
        textReport true
        lintConfig file("${rootProject.getRootDir()}/lint.xml")
        abortOnError true
        explainIssues false
        absolutePaths false
        disable 'NullSafeMutableLiveData'
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
            excludes += "/META-INF/versions/9/OSGI-INF/MANIFEST.MF"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        buildConfig true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:deprecation"
    }
    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(21))
            vendor.set(JvmVendorSpec.ADOPTIUM)
        }
    }
    namespace 'io.github.sheepdestroyer.materialisheep'

}

ext {
    okHttpVersion = '5.3.2'
    daggerVersion = '2.57.2'
    retrofitVersion = '3.0.0'
    roomVersion = '2.8.4'
    lifecycleVersion = '2.10.0'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.browser:browser:1.9.0'
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.2"
    implementation 'io.reactivex.rxjava3:rxjava:3.1.10'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation platform("org.jetbrains.kotlin:kotlin-bom:$rootProject.kotlin_version")
    implementation "org.jetbrains.kotlin:kotlin-stdlib"


    ksp "androidx.room:room-compiler:$roomVersion"
    ksp "com.google.dagger:dagger-compiler:$daggerVersion"
    kspTest "androidx.room:room-compiler:$roomVersion"
    kspTest "com.google.dagger:dagger-compiler:$daggerVersion"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.21.0'
    testImplementation 'org.robolectric:robolectric:4.16'
    testImplementation 'androidx.test:core:1.7.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}
